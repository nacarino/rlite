# See http://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# for more information on packaging from GIT sources.

# Maintainer: Vincenzo Maffione <v.maffione@gmail.com>
pkgname=rlite
pkgver=r57.be61446
pkgrel=1
pkgdesc="Recursive InterNetwork Architecture user/kernel prototype written in C."
arch=('any')
license=('BSD')
depends=('linux' 'glibc')
makedepends=('git' 'linux-headers' 'fakeroot')
install="rlite.install"
source=("rlite.install" "rlite.conf" "rlite.service" "git+https://github.com/vmaffione/rlite.git")
noextract=()
md5sums=("047aa5adec4c52ddbf86d12dbf300f71" "f42f582e7be0c99ed67a3cbe9e56c3ba" "ecaab3e792c1c36048a069334337b380" "SKIP")

_gitname="rlite"

pkgver() {
        cd "$srcdir/${pkgname%-git}"
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    RKVER=$(uname -r | sed 's|-.*||g')
    KMAJVER=$(echo "$RKVER" | sed 's|\.[0-9]\+$||g')
    msg "Building on kernel ${RKVER}..."

    cd "$srcdir/$_gitname"
    #./configure --with-vmpi || exit 1
    ./configure || exit 1
    make || exit 1
    msg "Build complete"
}

package() {
    # Compute the version numbers of the running kernel
    KVER1=$(uname -r)
    KVER2=$(uname -r | sed 's/\.[0-9]\+-[0-9]\+//')

    # Install the rina kernel modules into the VERSION/extra directory
    mkdir -p "$pkgdir/usr/lib/modules/${KVER1}/extra"
    cp $srcdir/$_gitname/kernel/*.ko "$pkgdir/usr/lib/modules/${KVER1}/extra"

    # Install the userspace programs and libraries
    mkdir -p "$pkgdir/usr/bin"
    mkdir -p "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/user/libs/librlite.so" "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/user/libs/librlite-cdap.so" "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/user/libs/librlite-conf.so" "$pkgdir/usr/lib"
    cp "$srcdir/$_gitname/build/user/uipcps/rlite-uipcps" "$pkgdir/usr/bin"
    cp "$srcdir/$_gitname/build/user/tools/rinaperf" "$pkgdir/usr/bin"
    cp "$srcdir/$_gitname/build/user/tools/rlite-ctl" "$pkgdir/usr/bin"
    cp "$srcdir/$_gitname/build/user/tools/rina-gw" "$pkgdir/usr/bin"

    # Install the rlite public headers
    mkdir -p "$pkgdir/usr/include/rlite"
    cp $srcdir/$_gitname/include/rlite/*.h "$pkgdir/usr/include/rlite"
    cp $srcdir/$_gitname/include/rlite/cdap.hpp "$pkgdir/usr/include/rlite"
    cp "$srcdir/$_gitname/build/user/libs/CDAP.pb.h" "$pkgdir/usr/include/rlite"

    # Install configuration files
    mkdir -p "$pkgdir/etc/rlite"
    cp $srcdir/$_gitname/user/uipcps/shim-tcp4-dir "$pkgdir/etc/rlite"

    # Install other system files
    mkdir -p "$pkgdir/var/rlite"
    mkdir -p "$pkgdir/etc/systemd/system"
    mkdir -p "$pkgdir/etc/modules-load.d"
    cp $srcdir/$_gitname/archlinux/rlite.service "$pkgdir/etc/systemd/system"
    cp $srcdir/$_gitname/archlinux/rlite.conf "$pkgdir/etc/modules-load.d"
}

# vim:set ts=2 sw=2 et:
